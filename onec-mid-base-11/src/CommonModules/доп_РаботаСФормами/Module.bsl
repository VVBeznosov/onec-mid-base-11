Процедура ДополнитьФорму (Форма) Экспорт

	ИмяФормы = Форма.ИмяФормы;
	
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		
		ДобавитьПолеВШапку(Форма);
		ДобавитьСогласованнуюСкидку(Форма);
	ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Или
				ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда 
				
		ДобавитьПолеВШапку(Форма)
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Или
				ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда 
				
		ДобавитьПолеПередСуммой(Форма)
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьПолеВШапку(Форма)
	
	ПолеВвода = Форма.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво); 
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.доп_КонтактноеЛицо"
	
КонецПроцедуры

Процедура ДобавитьПолеПередСуммой(Форма)
	
	ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"), , Форма.Элементы.СуммаДокумента);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.доп_КонтактноеЛицо"
	
КонецПроцедуры  

Процедура ДобавитьСогласованнуюСкидку(Форма)
	
	ГруппаСкидка = Форма.Элементы.Добавить("ГруппаСкидка", Тип("ГруппаФормы"),Форма.Элементы.ГруппаШапкаЛево);
	ГруппаСкидка.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаСкидка.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаСкидка.ОтображатьЗаголовок = Ложь; 
	ГруппаСкидка.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	
	ПолеВвода = Форма.Элементы.Добавить("СогласованнаяСкидка", Тип("ПолеФормы"), Форма.Элементы.ГруппаСкидка); 
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.доп_СогласованнаяСкидка"; 
	ПолеВвода.УстановитьДействие("ПриИзменении", "СкидкаПриИзменении");
	
	ПересчетКоманда = Форма.Команды.Добавить("ПересчитатьСкидку");
	ПересчетКоманда.Действие = "КомандаПересчитатьСкидку";
	ПересчетКоманда.Заголовок = "Пересчитать";

	КнопкаФормы = Форма.Элементы.Добавить("Пересчитать", Тип("КнопкаФормы"), Форма.Элементы.ГруппаСкидка);
	КнопкаФормы.ИмяКоманды = "ПересчитатьСкидку";
	КнопкаФормы.Заголовок = "Пересчитать";
	
КонецПроцедуры

